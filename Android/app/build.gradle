apply plugin: 'com.android.application'
apply from: 'constants.gradle'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.imobile3.groovypayments"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField STRING, STRIPE_API_CLIENT_KEY_FIELD, NULL
        buildConfigField STRING, STRIPE_API_SERVER_KEY_FIELD, NULL
    }

    buildTypes {
        debug {
            minifyEnabled false

            if (project.file("$project.rootDir/app/apikeys.gradle").exists()) {
                apply from: "$project.rootDir/app/apikeys.gradle"
                if (project.hasProperty('STRIPE_API_CLIENT_KEY')) {
                    buildConfigField STRING, STRIPE_API_CLIENT_KEY_FIELD, STRIPE_API_CLIENT_KEY
                }
                if (project.hasProperty('STRIPE_API_SERVER_KEY')) {
                    buildConfigField STRING, STRIPE_API_SERVER_KEY_FIELD, STRIPE_API_SERVER_KEY
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support Libs
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Paging
    implementation 'androidx.paging:paging-runtime:2.1.0'

    // ViewModel & LiveData
    def livedata_version = '2.0.0'
    implementation "androidx.lifecycle:lifecycle-runtime:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$livedata_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$livedata_version"

    // Room Database
    def room_version = "2.2.4"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Google Gson Serialization Library
    implementation 'com.google.code.gson:gson:2.8.5'

    // Square Retrofit HTTP Library - https://square.github.io/retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    // Stripe Java SDK (Server)
    implementation "com.stripe:stripe-java:17.16.0"

    // Stripe Android SDK (Client)
    implementation 'com.stripe:stripe-android:14.0.0'

    // Unit Testing Dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0-beta.5'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0-beta.5'
    testImplementation "androidx.room:room-testing:$room_version"

    // Instrumentation Testing Dependencies
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.google.guava:guava:26.0-android'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0-beta02'
    androidTestImplementation 'androidx.test:core:1.0.0-beta02'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0-beta02'
    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'

    // Chart/Graph Dependency
    implementation 'com.github.AnyChart:AnyChart-Android:1.1.2'
}
